*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        invalid_connection_details
Test Template     Invalid Connection Details Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    URL                 Username                 Password                 MailboxName        MailboxEmail
Create Shared Mailbox with Invalid URL
                      NOK       Invalid             ${OFFICE365_Username}    ${OFFICE365_Password}    ${Mailbox_Name}    ${Mailbox_Email}

Create Shared Mailbox with Invalid Username
                      NOK       ${OFFICE365_URL}    Invalid                  ${OFFICE365_Password}    ${Mailbox_Name}    ${Mailbox_Email}

Create Shared Mailbox with Invalid Password
                      NOK       ${OFFICE365_URL}    ${OFFICE365_Username}    Invalid                  ${Mailbox_Name}    ${Mailbox_Email}

*** Keywords ***
Startup
    Log    *******Start Test "INVALID CONNECTION DETAILS"*******
    Log    *******Connect to AE*******
    Connect AE

Invalid Connection Details Template
    [Arguments]    ${OK_NOK}    ${URL}    ${Username}    ${Password}    ${MailboxName}    ${MailboxEmail}
    [Documentation]    This action tests the feature of Create Shared Mailbox.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${URL}: Provide URL to connect to exchange server.
    ...    -${Username}: Provide username to connect to exchange server.
    ...    -${Password}: Provide password to connect to exchange server.
    ...    -${MailboxName}: Provide the name of the shared mailbox to be created.
    ...    -${MailboxEmail}: Provide the email address for the mailbox.
    Generate String
    Init Action    ${CREATE_SHARED_MAILBOX_ACTION}
    Run Keyword If    '${URL}' == 'Invalid'    Action Set    &UC4RB_OFC365_CONNURL#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OFC365_CONNURL#    ${URL}
    Run Keyword If    '${Username}' == 'Invalid'    Action Set    &UC4RB_OFC365_USERNAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OFC365_USERNAME#    ${Username}
    Run Keyword If    '${Password}' == 'Invalid'    Action Set    &UC4RB_OFC365_PASSWORD#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OFC365_PASSWORD    ${Password}
    Action Set    &UC4RB_OFC365_MAILBOXNAME#    ${MailboxName}
    Action Set    &UC4RB_OFC365_EMAIL#    ${MailboxEmail}
    Action Execute
    Assert Failure

Teardown
    Log    *******End Test "INVALID CONNECTION DETAILS"*******
