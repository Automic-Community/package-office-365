*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        delete_member
Test Template     Delete Member Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    Identity          Links       LinkType        AdvancedOptions
Delete members to group with all valid inputs
                      [Tags]    main_scenarios
                      OK        ${Identity}       ${Links}    ${Link_Type}    ${Adv_Options}

Delete members to group without advanced options
                      OK        ${Identity}       ${Links}    ${Link_Type}    ${EMPTY}

Delete members to group with invalid Identity
                      NOK       Invalid           ${Links}    ${Link_Type}    ${Adv_Options}

Delete members to group with invalid Links
                      NOK       ${Identity}       Invalid     ${Link_Type}    ${Adv_Options}

Delete members to group with invalid Advanced Options
                      NOK       ${Identity}       ${Links}    ${Link_Type}    Invalid

Delete members to group without Identity
                      NOK       ${EMPTY}          ${Links}    ${Link_Type}    ${Adv_Options}

Delete members to group without Links
                      NOK       ${Identity}       ${EMPTY}    ${Link_Type}    ${Adv_Options}

*** Keywords ***
Startup
    Log    *******Start Test "DELETE MEMBER"*******
    Log    *******Connect to AE*******
    Connect AE

Delete Member Template
    [Arguments]    ${OK_NOK}    ${Identity}    ${Links}    ${LinkType}    ${AdvancedOptions}
    [Documentation]    This action deletes the members from the group.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${Identity}: Provide the name or alias of the group.
    ...    -${Links}: Provide the unique identifier of the recipient (name, alias, email, Distinguished name, Canonical DN, GUID).
    ...    -${LinkType}: Provide the link type for the above link.
    ...    -${AdvancedOptions}: Provide advanced options supported by command.
    Generate String
    Init Action    ${DELETE_MEMBER}
    Connect To Office365
    Run Keyword If    '${Identity}' == 'Invalid'    Action Set    &UC4RB_OFFICE365_IDENTITY#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OFFICE365_IDENTITY#    ${Identity}
    Run Keyword If    '${Links}' == 'Invalid'    Action Set    &UC4RB_OFFICE365_LINKS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OFFICE365_LINKS#    ${Links}
    Run Keyword If    '${AdvancedOptions}' == 'Invalid'    Action Set    &UC4RB_OFFICE365_ADV_OPTS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OFFICE365_ADV_OPTS#    ${AdvancedOptions}
    Action Set    &UC4RB_OFFICE365_LINKTYPE#    ${LinkType}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "DELETE MEMBER"*******
