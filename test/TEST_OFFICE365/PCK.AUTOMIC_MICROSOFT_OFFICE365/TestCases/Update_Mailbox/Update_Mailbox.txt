*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        update_mailbox
Test Template     Update Mailbox Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    Identity             EmailAddresses      AdvancedOptions
Update Mailbox with all valid inputs
                      [Tags]    main_scenarios
                      OK        ${Mailbox_Name_2}    ${Mailbox_Email}    -MailTip "Test"

Update Mailbox without providing Email Addresses and Advanced Options
                      OK        ${Mailbox_Name_2}    ${EMPTY}            ${EMPTY}

Asserting behavior for Non-Existing Identity
                      NOK       Invalid              ${EMPTY}            ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "UPDATE MAILBOX"*******
    Log    *******Connect to AE*******
    Connect AE

Update Mailbox Template
    [Arguments]    ${OK_NOK}    ${Identity}    ${EmailAddresses}    ${AdvancedOptions}
    [Documentation]    This action tests the feature of Update Mailbox.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${Identity}: Provide the name of the mailbox to be updated.
    ...    -${EmailAddresses}: Provide the email address for the mailbox.
    ...    -${AdvancedOptions}: Provide advanced options in command.
    Generate String
    Init Action    ${UPDATE_MAILBOX_ACTION}
    Connect To Office365
    Run Keyword If    '${Identity}' == 'Invalid'    Action Set    &UC4RB_OFC365_IDENTITY#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OFC365_IDENTITY#    ${Identity}
    Action Set    &UC4RB_OFC365_EMAIL#    ${EmailAddresses}
    Action Set    &UC4RB_OFC365_ADV_PARAM#    ${AdvancedOptions}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "UPDATE MAILBOX"*******
